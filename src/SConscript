Import('env', 'ports')

build = env.Clone(BUILD = '.build')
build.Append(CPPFLAGS = ['-fno-exceptions']) # We use GCC/MinGW.
build.VariantDir('$BUILD', '.', duplicate = 0)

src = [
	'audio',
	'audio/backend',
	'audio/backend/openal',
	'audio/io',
	'base',
	'base/memory',
	'renderer',
	'renderer/builtin',
	'renderer/gl',
	'terminal',
	'image',
	'ion',
	'package']

if 'posix' in ports:
	src += [
		'base/posix',
		'terminal/x11']

if 'windows' in ports:
	src += [
		'base/windows',
		'terminal/windows']

yttrium = build.SharedLibrary('#/lib/yttrium', [build.Glob('$BUILD/' + path + '/*.cpp') for path in src])
build.Clean(yttrium, build.Dir('$BUILD'))

Return('yttrium')
